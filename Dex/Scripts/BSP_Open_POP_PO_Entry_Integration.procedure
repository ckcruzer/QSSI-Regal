Procedure	"BSP_Open_POP_PO_Entry_Integration"
{
	Core	"Purchasing"
	Documentation	""
	Script	"

local 	boolean 	l_ok;
local	integer		l_Ask;

{PO Number should have a value}
if empty('PO Number' of window POP_PO_Entry of form POP_PO_Entry) then
	warning \"Please enter a document number.\";
	abort script;
end if;

{Retrieve Settings}
release table BSP_Integration_SETP;
range clear table BSP_Integration_SETP;
clear table BSP_Integration_SETP;

set 'Company ID' of table BSP_Integration_SETP to 'Company ID' of globals;
if err() <> OKAY then
	warning \"Dynamics GP Integration setting is missing!\";
	abort script;
end if;


if (changed(window POP_PO_Entry of form POP_PO_Entry) or '(L) fLineChanged' of window POP_PO_Entry of form POP_PO_Entry) then
	if not empty('PO Number' of window POP_PO_Entry of form POP_PO_Entry) then
		if not QueryToSave(POP_POState of window POP_PO_Entry of form POP_PO_Entry, table POP_PO) of form POP_PO_Entry then
			abort close;
			abort script;
		end if;
	else	{ If the user blanked the field, then we must manually ask }
		case ask(getmsg(8137), getmsg(SAVE), getmsg(DELETE), getmsg(CANCEL), 0)
			in [ASKBUTTON1]
				{ This will **ALWAYS** fail since the PO # is not set, but leave
				  the call in to consolidate code in 1 place to give message. }
				if (not Save(POP_POState of window POP_PO_Entry of form POP_PO_Entry, table POP_PO,false) of form POP_PO_Entry) then
					abort close;
					abort script;				
				end if;
			{ Let Delete fall thru since the user wanted to discard changes
			  anyway, treat it as if the window has not been changed }		
			in [ASKBUTTON3]	{ Cancel }
				abort close;
				abort script;
		end case;
	end if;
end if;

open form BSP_POP_PO_Entry_Integration;
set 'PO Number' of window BSP_POP_PO_Entry_Integration of form BSP_POP_PO_Entry_Integration to 
	'PO Number' of window POP_PO_Entry of form POP_PO_Entry;
run script 'Display Existing Record' of window BSP_POP_PO_Entry_Integration of form BSP_POP_PO_Entry_Integration;	"
	ServiceAction	""
	ServiceCustomAciton	""
	ServiceEnabled	"FALSE"
	ServiceRawRequest	"FALSE"
	ServiceUriTemplate	""
	UserProperties	""
}
