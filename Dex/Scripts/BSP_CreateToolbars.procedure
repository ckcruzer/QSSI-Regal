Procedure	"BSP_CreateToolbars"
{
	Core	"System"
	Documentation	""
	Script	"{-----------------------------------------------------------------------
DESCRIPTION:
This procedure creates the toolbar for the BSP integrating
application.
------------------------------------------------------------------------
Copyright (c) Microsoft Corporation. All rights reserved.
------------------------------------------------------------------------}

local string Current_User;

{Add command bar only if it doesn't exist for the user}
if ExistsForUserID(\"\",
		Runtime_GetCurrentProductID(),
		resourceid(form Command_BSP), 
		resourceid(command CL_BSP_CMDBAR of form Command_BSP)) of form syCmdBarObj = true then
	abort script;
end if;	

{Add the toolbar and clone for each user who has toolbars}
{Third-party products can't use the built-in cloning for this release}

{Look through all the users in the table}
range clear table syCmdBarOptions;

{Clear the Current_User variable}
Current_User = \"@@\";

{Get the first record}
get first table syCmdBarOptions;

while err() <> EOF do

	if Current_User <> 'User ID' of table syCmdBarOptions then
		{Set the Current_User variable}
		Current_User = 'User ID' of table syCmdBarOptions;
	
		{Have a new user, so create the toolbar for the current user}
		call BSP_CreateToolbarForUser, 
			Current_User,
			Runtime_GetCurrentProductID(),
			resourceid(form Command_BSP), 
			resourceid(command CL_BSP_CMDBAR of form Command_BSP),
			true, {visible by default}
			true; {Want this on a new row}	
			
	end if;

	{Get the next record}
	get next table syCmdBarOptions;

end while;

"
	ServiceAction	""
	ServiceCustomAciton	""
	ServiceEnabled	"FALSE"
	ServiceRawRequest	"FALSE"
	ServiceUriTemplate	""
	UserProperties	""
}
