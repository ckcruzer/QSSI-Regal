Procedure	"BSP_SOP_CreateSalesBatch"
{
	Core	"Sales"
	Documentation	""
	Script	"in		'Batch Number'		IN_BatchNumber;
in 		'Batch Comment'		IN_BatchComment;

local	integer				nStatus;
local 	BatchState			Batch;
local	BatchIdxSrcByNum	BatchIdx;
local 	BatchData			BatchData;

{ Refresh the same document type and recreate the batch if posting removed it }
nStatus = Create(Batch, table Batch_Headers, 0, \"\", \"\") of form Batch;
assert nStatus = OKAY;

BatchIdx:'Batch Source' = BATCH_SOURCE_SOP_ENTRY;			
BatchIdx:'Batch Number' = IN_BatchNumber;
call SetIndexSrcByNum of form Batch,
			Batch, 
			table Batch_Headers,
			BatchIdx;
			
nStatus = Get(Batch, table Batch_Headers, CHG + EQUAL) of form Batch;
if nStatus = MISSING then
	{ Batch does not exist so create it }
	nStatus = Get(Batch, table Batch_Headers, ADDNEW + EQUAL) of form Batch;
	if nStatus = OKAY then
		{this is the header information except the totals}
		clear BatchData;
		set BatchData:'Batch Source' to BATCH_SOURCE_SOP_ENTRY;
		set BatchData:'Batch Number' to IN_BatchNumber;
		set BatchData:'Batch Comment' to IN_BatchComment;
		set BatchData:Series to SALES;
		set BatchData:Origin to 1;
		set BatchData:'Batch Frequency' to 1;
		set BatchData:'Marked To Post' to false;
		set BatchData:'Batch Status' to 0; 
		set BatchData:'User ID' to 'User ID' of globals;
		set BatchData:'Batch Date' to sysdate();
		set BatchData:'GL Posting Date' to sysdate();
		set BatchData:'Post To GL' to false;
		set BatchData:'Created Date' to sysdate();
		set BatchData:'Time' to systime();
		
		call SetAll of form Batch, Batch, table Batch_Headers, BatchData;
		
		nStatus = Commit(Batch,
						table Batch_Headers) of form Batch;
		assert nStatus = OKAY;
			
	else
		assert (false);
				
	end if;
	
else
	{ Do we need to do anything }
	call Release of form Batch,
				Batch,
				table Batch_Headers;
end if;
	
call Destroy of form Batch, 
	Batch, 
	table Batch_Headers;


{ Postconditions }
"
	ServiceAction	""
	ServiceCustomAciton	""
	ServiceEnabled	"FALSE"
	ServiceRawRequest	"FALSE"
	ServiceUriTemplate	""
	UserProperties	""
}
