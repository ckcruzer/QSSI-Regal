Procedure	"BSP_CreateToolbarForUser"
{
	Core	"System"
	Documentation	""
	Script	"{-----------------------------------------------------------------------
DESCRIPTION:
This procedure creates a toolbar for the specified user. The procedure
is called once for each user that has toolbars defined. The last
two parameters specify whether the new toolbar is visible by default,
and whether the new toolbar will appear at the end of the last row of
toolbars, or on its own row.
------------------------------------------------------------------------
Copyright (c) Microsoft Corporation. All rights reserved.
------------------------------------------------------------------------}

in string UserID;
in integer ProductID;
in integer FormID;
in integer CommandListID;
in boolean Visible;
in boolean NewRow;

local integer Seq;
local integer Row;
local integer status;

{First, determine where to add the toolbar for the default case}
range clear table syCmdBarOptions;

{Set the starting range}
clear table syCmdBarOptions;
'User ID' of table syCmdBarOptions = UserID;
range start table syCmdBarOptions by syCmdBarOptionsIdx_Seq;

{Set the ending range}
fill table syCmdBarOptions;
'User ID' of table syCmdBarOptions = UserID;
range end table syCmdBarOptions by syCmdBarOptionsIdx_Seq;

{Find the next sequence number.}
get last table syCmdBarOptions by syCmdBarOptionsIdx_Seq;
Seq = RowSequence of table syCmdBarOptions + 1;

{Find the next available row number}
while ('Row Number' of table syCmdBarOptions = 99) and (err() <> EOF) do
	{Look at the previous record}
	get prev table syCmdBarOptions by syCmdBarOptionsIdx_Seq;	
end while;

{Increment the row number to use a new row, if indicated}
Row = 'Row Number' of table syCmdBarOptions;
if NewRow = true then
	Row = Row + 1;
end if;

status = AddCommandBar(UserID, 
	ProductID, 
	FormID, 
	CommandListID,
	Visible, {visibility}
	Row, {row number}
	Seq, {sequence}
	false) of form syCmdBarObj;"
	ServiceAction	""
	ServiceCustomAciton	""
	ServiceEnabled	"FALSE"
	ServiceRawRequest	"FALSE"
	ServiceUriTemplate	""
	UserProperties	""
}
